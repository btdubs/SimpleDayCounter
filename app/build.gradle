// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  namespace 'com.brianco.simpledaycounter'

  compileSdkVersion versions.compileSdk

  defaultConfig {
    applicationId namespace

    minSdkVersion 26
    targetSdkVersion 35

    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "$versionMajor.$versionMinor.$versionPatch"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = '1.8'
  }

  lint {
    abortOnError true
    warningsAsErrors true
    lintConfig rootProject.file('lint.xml')
    textReport true
    htmlReport true
    htmlOutput rootProject.file("lint-reports/${name}.html")
    // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks.
    checkReleaseBuilds false
  }

  buildTypes {
    debug {
      debuggable true
    }
    release {
      // Enables code shrinking, obfuscation, and optimization for only
      // your project's release build type. Make sure to use a build
      // variant with `debuggable false`.
      minifyEnabled true

      // Enables resource shrinking, which is performed by the
      // Android Gradle plugin.
      shrinkResources true

      // Includes the default ProGuard rules files that are packaged with
      // the Android Gradle plugin. To learn more, go to the section about
      // R8 configuration files.
      proguardFiles getDefaultProguardFile(
          'proguard-android-optimize.txt'),
          'proguard-rules.pro'
    }
  }

  productFlavors {
    flavorDimensions = ['environment']

    internal {
      applicationIdSuffix '.internal'
      versionNameSuffix '-internal'
    }

    production {
    }
  }

  applicationVariants.configureEach { variant ->
    variant.outputs.configureEach { output ->
      outputFileName = "Simple Day Counter ${variant.versionCode}.apk"
      if (variant.flavorName == 'internal') {
        outputFileName = 'Internal ' + outputFileName
      }
    }
  }
}

dependencies {
  implementation deps.material
  implementation deps.androidx.activity
  implementation deps.androidx.annotations
  implementation deps.androidx.appcompat
  implementation deps.androidx.collection
  implementation deps.androidx.core
  implementation deps.androidx.fragment
  implementation deps.androidx.lifecycleLivedataCore
  implementation deps.androidx.lifecycleRuntime
  implementation deps.androidx.lifecycleViewmodel
  implementation deps.androidx.preference
  implementation deps.androidx.savedstate
  implementation deps.kotlin.stdlibJdk8

  internalImplementation deps.diskcrashreporter
  internalImplementation deps.leakcanary

  testImplementation deps.junit
  testImplementation deps.truth
}
